import com.android.build.gradle.internal.api.ApkVariantOutputImpl
import com.android.build.gradle.internal.tasks.FinalizeBundleTask
import org.jetbrains.kotlin.util.capitalizeDecapitalize.capitalizeAsciiOnly
import java.util.Properties

plugins {
  id("target-compose")
  id("target-common")
  id(libs.plugins.androidApplication.get().pluginId)
  alias(libs.plugins.androidxBaselineprofile)
}

kotlin {
  kmpComposeTarget(project) {
    dependencies {
      implementation(projects.shared) // ç”¨äºå¯¼å…¥ resources æ–‡ä»¶å¤¹
      implementation(projects.helper)
      implementation(projects.helperCompose)
      implementation(projects.helperPlatform)
      implementation(projects.pureIO)
      implementation(projects.pureHttp)
      implementation(projects.pureImage)
      implementation(projects.core)
      implementation(projects.window)
      implementation(projects.sys)
      implementation(projects.browser)
      implementation(projects.dwebview)
    }
  }
  kmpAndroidTarget(project) {
    dependencies {
      implementation(libs.androidx.core.splashscreen)
      // Google.accompanist ç›¸å…³
      implementation(libs.accompanist.webview)
      implementation(libs.accompanist.permissions)
      implementation(libs.accompanist.systemui.controller)

      implementation(libs.compose.ui.preview)
    }
  }
}

val keystoreProperties = Properties()
val keystorePropertiesFile = project.file("key.properties")
if (keystorePropertiesFile.exists()) {
  keystoreProperties.load(keystorePropertiesFile.inputStream())
}

android {
  namespace = "info.bagen.dwebbrowser"
  compileSdk = libs.versions.compileSdkVersion.get().toInt()
  val localProperties = localProperties()
  defaultConfig {
    applicationId = "info.bagen.dwebbrowser"
    minSdk = libs.versions.minSdkVersion.get().toInt()
    targetSdk = libs.versions.targetSdkVersion.get().toInt()
    versionCode = libs.versions.versionCode.get().toInt()
    versionName = libs.versions.versionName.get()


    val needarmeabiv7a = localProperties.getBoolean("android.build.ndk.armeabi-v7a")
    val needx86 = localProperties.getBoolean("android.build.ndk.x86")

    ndk.abiFilters.addAll(
      listOf("arm64-v8a").let {
        if (needarmeabiv7a) it + "armeabi-v7a" else it
      }.let {
        if (needx86) it + "x86" else it
      })
  }
  baselineProfile {
    mergeIntoMain = true
    dexLayoutOptimization = true
    saveInSrc = true
    baselineProfileOutputDir = "generated/baselineProfiles"
    // ./gradlew androidApp:generateBaselineProfile
    automaticGenerationDuringBuild = false
  }

  packaging {
    resources {
      excludes += "/META-INF/versions/9/previous-compilation-data.bin"
      excludes += "/META-INF/{AL2.0,LGPL2.1}"
// æ·»åŠ  http4k æ¡†æ¶åï¼Œä¼šæœ‰å¼‚å¸¸æŠ¥é”™ï¼Œéœ€è¦æ·»åŠ å¦‚ä¸‹å†…å®¹
      excludes += "/META-INF/INDEX.LIST"
      excludes += "/META-INF/DEPENDENCIES"
    }
  }

  // è·å–æœ¬åœ°é…ç½®æ–‡ä»¶
  val props = Properties().also { properties ->
    rootDir.resolve("local.properties").apply {
      if (exists()) {
        inputStream().use { properties.load(it) }
      }
    }
  }

  signingConfigs {
    create("release") {
// ä½¿ç”¨ keytool -printcert -jarfile app_release.apk ç›´æ¥æ‰“å° jar ç­¾åä¿¡æ¯
// ä½¿ç”¨ jarsigner --verify app_release.apk æç¤º"jar å·²éªŒè¯ã€‚"
// ä½¿ç”¨ apksigner verify -v app_release.apk æ˜¾ç¤ºå¦‚ä¸‹ï¼ŒV1 ä¸ºfalseï¼Œä½†å®é™…æ˜¯æœ‰ç­¾åçš„ã€‚åªæœ‰å½“minSdkæ”¹ä¸º<24çš„ç‰ˆæœ¬ï¼Œè¿™è¾¹çš„éªŒè¯æ‰ä¼šæç°ä¸ºtrue
//    Verified using v1 scheme (JAR signing): false
//    Verified using v2 scheme (APK Signature Scheme v2): true
//    Verified using v3 scheme (APK Signature Scheme v3): false
//    Verified using v3.1 scheme (APK Signature Scheme v3.1): false
      enableV1Signing = true
      enableV2Signing = true
      enableV3Signing = false
      enableV4Signing = false

      keyAlias = keystoreProperties["keyAlias"]?.toString()
      keyPassword = keystoreProperties["keyPassword"]?.toString()
      storeFile = keystoreProperties["storeFile"]?.let { file(it.toString()) }
      storePassword = keystoreProperties["storePassword"]?.toString()
      println("qqqq storeFile=${storeFile}")
    }
    getByName("debug") {
      // è·å–æœ¬åœ°é…ç½®çš„ key ä¿¡æ¯ï¼ŒstoreFile æ˜¯å°†jksæ–‡ä»¶æ”¾åœ¨å½“å‰ build.gradle.kts åŒçº§ç›®å½•
      props.getProperty("keyAlias", null)?.let { keyAlias = it }
      props.getProperty("keyPassword", null)?.let { keyPassword = it }
      props.getProperty("storeFile", null)?.let { storeFile = file(it) }
      props.getProperty("storePassword", null)?.let { storePassword = it }
    }
  }

  android.buildFeatures.buildConfig = true
  buildTypes {
    getByName("release") {
      // signingConfig = signingConfigs.getByName("debug") // å¦‚æœæ˜¯æµ‹è¯•benchmarkéœ€è¦ä½¿ç”¨debug
      signingConfig = signingConfigs.getByName("release")
      isMinifyEnabled = true // å¼€å¯ä»£ç æ··æ·†
      setProguardFiles(listOf("proguard-rules.pro"))
      isShrinkResources = true // ç§»é™¤æ— ç”¨çš„resource
      resValue("string", "appName", "Dweb Browser")
      applicationIdSuffix = null
      versionNameSuffix = null
    }
    getByName("debug") {
      signingConfig = signingConfigs.getByName("debug")
      val userName = props.getProperty("user.name", null)
        ?: System.getProperty("user.name").replace("[^a-zA-Z0-9]".toRegex(), "").lowercase()
      resValue("string", "appName", "Kmp-$userName")
      applicationIdSuffix = ".kmp.$userName"
      versionNameSuffix = null // ".kmp.$userName"
    }
    create("benchmark") {
      initWith(buildTypes.getByName("release"))
      matchingFallbacks += listOf("release")
      isDebuggable = false
    }
  }

  flavorDimensions += listOf("abi")
  productFlavors {
//    create("debug") {
//      signingConfig = signingConfigs.getByName("debug")
//      resValue("string", "appName", "ğŸ§ªDwebBrowser")
//      applicationIdSuffix = ".debug"
//    }

//    if (localProperties.getBoolean("android.build.ndk.armeabi-v7a")) {
    create("withArm32") {
      dimension = "abi"
      matchingFallbacks += listOf("release")
      ndk.abiFilters.addAll(listOf("arm64-v8a", "armeabi-v7a"))
    }
//    }
//    if (localProperties.getBoolean("android.build.ndk.x86")) {
    create("withX86") {
      dimension = "abi"
      matchingFallbacks += listOf("release")
      ndk.abiFilters.addAll(listOf("arm64-v8a", "x86"))
    }
//    }
  }
  applicationVariants.all {
    outputs.all {
      val midName = when {
        name.startsWith("with") -> "_" + name.substring(4).split("-").first().lowercase()
        else -> ""
      }
      val archivesName = "DwebBrowser${midName}_v${libs.versions.versionName.get()}"

      if (buildType.name == "release") {
        // ä¿®æ”¹bundleå
        val bundleFinalizeTaskName = StringBuilder("sign").run {
          append(flavorName.capitalizeAsciiOnly())
          append(buildType.name.capitalizeAsciiOnly())
          append("Bundle")
          toString()
        }
        tasks.named(bundleFinalizeTaskName, FinalizeBundleTask::class) {
          val file = finalBundleFile.asFile.get()
          finalBundleFile.set(File(file.parentFile, "$archivesName.aab"))
        }

        // ä¿®æ”¹apkå
        if (this is ApkVariantOutputImpl) {
          outputFileName = "$archivesName.apk"
        }
      } else {
        // ä¿®æ”¹apkå
        if (this is ApkVariantOutputImpl) {
          outputFileName = "${archivesName}_debug.apk"
        }
      }
    }
  }
}

dependencies {
  implementation(libs.androidx.profile.installer)
//  baselineProfile(projects.androidBenchmark)
  baselineProfile(projects.androidBaselineprofile)
}